<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_SlaveCtrlInstruManualTest" Id="{dbf632a1-f924-4c4a-9c08-3e3912584188}" SpecialFunc="None">
    <Declaration><![CDATA[// Brief: Slave Arm test
FUNCTION_BLOCK PUBLIC FB_SlaveCtrlInstruManualTest EXTENDS FB_SlaveArmCtrlBase
VAR
																
	// final target DH joint position and grip angle
	m_targetDHJntPos:Vec3d;
	m_targetGripAng:LREAL;
	m_targetGripForceLevel :LREAL :=0;
	
	// interploated command joint position and grip angle 
	m_cmdDHJntPos :Vec6d;
	m_cmdDHJntVel :Vec6d;
	m_cmdGripAng:LREAL;

	// last joint position 
	m_lastCmdJntPos :Vec8d ;
	
	// instrument controller
	m_instruController : FB_BasicInstruControl;
	m_usKnifeController : FB_UltrosonicKnifeControl;
	m_instruCmdJntPos,m_instruCmdJntVel : Vec5d;
	m_instruCmdJntTrq :Vec5d;
	
	// OTG
	m_DHJntOTG:ARRAY[1..3] OF FB_secOrdTrajFilter;
	m_gripAngleOTG : FB_secOrdTrajFilter;
	m_maxJntVel:Vec3d:=[3(2.0)];
	m_maxJntAcc:Vec3d:=[3(4.0)];
	
	// instrument tyep
	m_instruType : E_InstrumentType;
	bStart: BOOL;
	iStep: INT;
	fbTon: TON;

	m_maxGripCurrent:LREAL:=350.0;
	m_maxGripCtrlCurrent:LREAL:=540.0;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="calcCmdJntPos" Id="{9ac6c1d4-8249-4e40-93a3-6d5814eb3008}">
      <Declaration><![CDATA[METHOD PROTECTED calcCmdJntPos : BOOL
VAR_IN_OUT CONSTANT
	i_endoscopePose : ST_Frame;
	i_masterStatus :ST_MasterStatus;
	i_setupStatus: ST_SetupStatus;
	i_slaveArm :FB_SlaveArm;
END_VAR

VAR 
	i:INT :=0;
	errThres :LREAL;
	m_targetDHJntPosLast: Vec3d;
	m_curCmdDHJntPos: Vec3d;
	m_compensationAngleDH: Vec3d;
	m_targetGripAngLast: LREAL;
	m_curCmdAng: LREAL;

END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// determine target DH joint position and gripper motion
IF GVL_InstruTestUIVars.g_moveToSetPosFlag THEN
	m_targetDHJntPos[1] := GVL_InstruTestUIVars.g_targetRollAng*g_deg2rad;
	m_targetDHJntPos[2] := (GVL_InstruTestUIVars.g_targetPitchAng-90)*g_deg2rad;
	m_targetDHJntPos[3] := GVL_InstruTestUIVars.g_targetYawAng*g_deg2rad;
	IF GVL_InstruTestUIVars.g_targetGripAng>-15 THEN 
		m_targetGripAng := GVL_InstruTestUIVars.g_targetGripAng*g_deg2rad;
		m_targetGripForceLevel:=0;
	ELSE
		m_targetGripAng := -15*g_deg2rad;
		m_targetGripForceLevel := LIMIT(0, ABS(GVL_InstruTestUIVars.g_targetGripAng), 100);
	END_IF
ELSIF GVL_InstruTestUIVars.g_gripperOpenFlag THEN
	m_targetGripAng := i_slaveArm.instrument.instruTypeInfo.m_maxGripAngle;
	m_targetGripForceLevel :=0;
ELSIF GVL_InstruTestUIVars.g_gripperCloseFlag THEN
	m_targetGripAng := 0;
	m_targetGripForceLevel :=0;
ELSIF GVL_InstruTestUIVars.g_maxGripForceFlag THEN
	m_targetGripAng := -45*g_deg2rad;
	m_targetGripForceLevel :=100;
END_IF

IF GVL_InstruTestUIVars.m_cycleStart THEN 
	bStart:=TRUE;
	GVL_InstruTestUIVars.m_cycleStart:=FALSE;
END_IF
IF GVL_InstruTestUIVars.m_cycleStop THEN 
	bStart:=FALSE;
	GVL_InstruTestUIVars.m_cycleStop:=FALSE;
END_IF
fbTon(IN:=iStep=10,PT:=T#1S);
IF bStart THEN
	CASE iStep OF 
		0:
			m_targetGripAng := i_slaveArm.instrument.instruTypeInfo.m_maxGripAngle;
			m_targetGripForceLevel :=0;
			iStep:=5;
		5:
		    IF ABS(m_cmdGripAng- m_targetGripAng)< g_jntCtrlAngTolLow THEN
				iStep:=10;
			END_IF
		10:
			IF fbTon.Q THEN
				m_targetGripAng := -90*g_deg2rad;
				m_targetGripForceLevel :=0;
				iStep:=15;
			END_IF
		15:
		    IF ABS(m_cmdGripAng- m_targetGripAng)< g_jntCtrlAngTolLow THEN
				iStep:=0;
			END_IF
	END_CASE
END_IF
// run OTG
FOR i:=1 TO 3  DO
	m_DHJntOTG[i].run(m_targetDHJntPos[i],0.0, o_cmdAcc=>,o_cmdVel=>m_cmdDHJntVel[i],o_cmdPos=> m_cmdDHJntPos[i+3]);
END_FOR
m_gripAngleOTG.run(m_targetGripAng,0.0, o_cmdAcc=>,o_cmdVel=>,o_cmdPos=>m_cmdGripAng);

// calculate target motor joint position from DH joint position
i_slaveArm.instrument.instruIK(m_cmdDHJntPos,m_cmdGripAng, o_jntPos=>m_cmdJntPos);

// command velocity
vecSub(m_cmdJntPos, m_lastCmdJntPos, m_cmdJntVel);
vecSelfMultiNum(m_cmdJntVel, g_slaveArmCtrlFreq);
m_lastCmdJntPos:=m_cmdJntPos;]]></ST>
      </Implementation>
    </Method>
    <Method Name="calcCmdJntTrq" Id="{672b1be1-2807-47dd-8f1d-525fb978a27b}">
      <Declaration><![CDATA[// override this function in each exact controller
METHOD PROTECTED calcCmdJntTrq : BOOL
VAR_IN_OUT CONSTANT
	i_endoscopePose : ST_Frame;
	i_masterStatus :ST_MasterStatus;
	i_setupStatus: ST_SetupStatus;
	i_slaveArm :FB_SlaveArm;
END_VAR

VAR 
	nullspaceMat :Mat7d;
	ctrlTrq : Vec7d;
	i : int ;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// set joint torque to zero in default
m_cmdJntTrq := g_zeroVec8d;

// instrument joint control
FOR i:=1 TO 5 DO 
	m_instruCmdJntPos[i]:=m_cmdJntPos[i+3];
	m_instruCmdJntVel[i]:=m_cmdJntVel[i+3];
END_FOR

IF i_slaveArm.instrument.instruType = InstruType_UltrasoundKnifeS OR i_slaveArm.instrument.instruType = InstruType_UltrasoundKnifeU THEN
	m_usKnifeController.run(m_instruCmdJntPos,m_instruCmdJntVel, m_targetGripForceLevel, i_slaveArm, o_cmdJntTrq=>m_instruCmdJntTrq);
ELSE
	m_instruController.run(m_instruCmdJntPos,m_instruCmdJntVel, m_cmdDHJntPos[6], m_cmdDHJntVel[6], m_targetGripForceLevel, i_slaveArm, o_cmdJntTrq=>m_instruCmdJntTrq);
END_IF

FOR i:=1 TO 5 DO
	m_cmdJntTrq[i+3]:=m_instruCmdJntTrq[i];
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="init" Id="{406632cf-8059-46ab-b0c2-acbe734ef5ab}">
      <Declaration><![CDATA[METHOD PUBLIC init : BOOL
VAR_IN_OUT CONSTANT
	i_slaveArm	:FB_SlaveArm;
END_VAR
VAR_IN_OUT 
	r_slaveArmCtrlCmd :ST_SlaveArmCtrlCmds;
END_VAR

VAR
	i:int;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.Init(i_slaveArm,r_slaveArmCtrlCmd);
m_lastCmdJntPos:=m_cmdJntPos;
m_jntOPMode[1] := DriverOPMode_PosTrq;
m_jntOPMode[2] := DriverOPMode_PosTrq;
m_jntOPMode[3] := DriverOPMode_PosTrq;
m_jntOPMode[4] := DriverOPMode_Trq;
m_jntOPMode[5] := DriverOPMode_Trq;
m_jntOPMode[6] := DriverOPMode_Trq;
m_jntOPMode[7] := DriverOPMode_Trq;
m_jntOPMode[8] := DriverOPMode_Trq;

m_cmdDHJntPos :=i_slaveArm.curDHJntPos;
m_cmdDHJntVel :=g_zeroVec6d;
m_targetGripAng := i_slaveArm.curGripAngle;
m_targetDHJntPos[1] := i_slaveArm.curDHJntPos[4];
m_targetDHJntPos[2] := i_slaveArm.curDHJntPos[5];
m_targetDHJntPos[3] := i_slaveArm.curDHJntPos[6];

// init instrument controller
m_instruController.init(i_slaveArm,m_maxGripCurrent,m_maxGripCtrlCurrent);
m_usKnifeController.init(i_slaveArm);
iStep:=0;
bStart:=FALSE;
IF GVL_InstruTestUIVars.g_rotationAngularVel=0 THEN GVL_InstruTestUIVars.g_rotationAngularVel:=5;END_IF
IF GVL_InstruTestUIVars.g_pitchAngularVel=0 THEN GVL_InstruTestUIVars.g_pitchAngularVel:=5;END_IF
IF GVL_InstruTestUIVars.g_deviationAngularVel=0 THEN GVL_InstruTestUIVars.g_deviationAngularVel:=5;END_IF
IF GVL_InstruTestUIVars.g_rotationAngularAcc=0 THEN GVL_InstruTestUIVars.g_rotationAngularAcc:=30;END_IF
IF GVL_InstruTestUIVars.g_pitchAngularAcc=0 THEN GVL_InstruTestUIVars.g_pitchAngularAcc:=30;END_IF
IF GVL_InstruTestUIVars.g_deviationAngularAcc=0 THEN GVL_InstruTestUIVars.g_deviationAngularAcc:=30;END_IF
FOR i:=1 TO 3  DO
	m_maxJntVel[i]:=5;
	m_maxJntAcc[i]:=30;
	IF GVL_InstruTestUIVars.g_account.actualPermission=2 THEN
		m_maxJntVel[1]:=GVL_InstruTestUIVars.g_rotationAngularVel;
		m_maxJntVel[2]:=GVL_InstruTestUIVars.g_pitchAngularVel;
		m_maxJntVel[3]:=GVL_InstruTestUIVars.g_deviationAngularVel;
		m_maxJntAcc[1]:=GVL_InstruTestUIVars.g_rotationAngularAcc;
		m_maxJntAcc[2]:=GVL_InstruTestUIVars.g_pitchAngularAcc;
		m_maxJntAcc[3]:=GVL_InstruTestUIVars.g_deviationAngularAcc;
	END_IF
END_FOR

FOR i:=1 TO 3  DO
	m_DHJntOTG[i].init(m_cmdDHJntPos[i+3], 0, m_maxJntVel[i], m_maxJntAcc[i], g_slaveArmCtrlCycleTime);
END_FOR

m_gripAngleOTG.init(m_targetGripAng, 0, 2*m_maxJntVel[3], 2*m_maxJntAcc[3], g_slaveArmCtrlCycleTime);
// update commands to arm
copyCmds(r_slaveArmCtrlCmd);


]]></ST>
      </Implementation>
    </Method>
    <Property Name="InstruBacklash" Id="{b8a24ea9-b582-4c33-9d96-935e62686f4d}">
      <Declaration><![CDATA[PROPERTY InstruBacklash : Vec5d]]></Declaration>
      <Get Name="Get" Id="{b3292599-741b-4cc2-b2a4-345b2cfdea2c}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF m_instruType = InstruType_UltrasoundKnifeS OR m_instruType = InstruType_UltrasoundKnifeU THEN
	instruBacklash := g_zeroVec5d;
ELSE
	instruBacklash := m_instruController.m_jntBacklash;
END_IF]]></ST>
        </Implementation>
      </Get>
    </Property>
    <LineIds Name="FB_SlaveCtrlInstruManualTest">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_SlaveCtrlInstruManualTest.calcCmdJntPos">
      <LineId Id="443" Count="0" />
      <LineId Id="428" Count="3" />
      <LineId Id="914" Count="1" />
      <LineId Id="917" Count="3" />
      <LineId Id="916" Count="0" />
      <LineId Id="434" Count="8" />
      <LineId Id="326" Count="0" />
      <LineId Id="826" Count="0" />
      <LineId Id="848" Count="6" />
      <LineId Id="847" Count="0" />
      <LineId Id="893" Count="0" />
      <LineId Id="855" Count="1" />
      <LineId Id="858" Count="0" />
      <LineId Id="860" Count="6" />
      <LineId Id="868" Count="0" />
      <LineId Id="894" Count="0" />
      <LineId Id="869" Count="2" />
      <LineId Id="895" Count="0" />
      <LineId Id="872" Count="2" />
      <LineId Id="867" Count="0" />
      <LineId Id="859" Count="0" />
      <LineId Id="857" Count="0" />
      <LineId Id="391" Count="3" />
      <LineId Id="261" Count="0" />
      <LineId Id="397" Count="0" />
      <LineId Id="396" Count="0" />
      <LineId Id="164" Count="0" />
      <LineId Id="287" Count="0" />
      <LineId Id="211" Count="0" />
      <LineId Id="187" Count="1" />
      <LineId Id="131" Count="0" />
    </LineIds>
    <LineIds Name="FB_SlaveCtrlInstruManualTest.calcCmdJntTrq">
      <LineId Id="121" Count="0" />
      <LineId Id="34" Count="1" />
      <LineId Id="135" Count="0" />
      <LineId Id="137" Count="1" />
      <LineId Id="161" Count="0" />
      <LineId Id="139" Count="0" />
      <LineId Id="195" Count="4" />
      <LineId Id="141" Count="0" />
      <LineId Id="200" Count="0" />
      <LineId Id="142" Count="2" />
      <LineId Id="177" Count="0" />
    </LineIds>
    <LineIds Name="FB_SlaveCtrlInstruManualTest.init">
      <LineId Id="13" Count="0" />
      <LineId Id="101" Count="0" />
      <LineId Id="86" Count="6" />
      <LineId Id="19" Count="0" />
      <LineId Id="120" Count="0" />
      <LineId Id="84" Count="1" />
      <LineId Id="93" Count="0" />
      <LineId Id="175" Count="1" />
      <LineId Id="174" Count="0" />
      <LineId Id="81" Count="2" />
      <LineId Id="162" Count="0" />
      <LineId Id="195" Count="0" />
      <LineId Id="218" Count="0" />
      <LineId Id="230" Count="5" />
      <LineId Id="188" Count="0" />
      <LineId Id="191" Count="0" />
      <LineId Id="196" Count="0" />
      <LineId Id="192" Count="0" />
      <LineId Id="198" Count="0" />
      <LineId Id="194" Count="0" />
      <LineId Id="199" Count="0" />
      <LineId Id="201" Count="1" />
      <LineId Id="206" Count="0" />
      <LineId Id="189" Count="0" />
      <LineId Id="148" Count="0" />
      <LineId Id="197" Count="0" />
      <LineId Id="128" Count="0" />
      <LineId Id="149" Count="1" />
      <LineId Id="110" Count="0" />
      <LineId Id="136" Count="0" />
      <LineId Id="111" Count="0" />
      <LineId Id="109" Count="0" />
      <LineId Id="80" Count="0" />
      <LineId Id="59" Count="0" />
      <LineId Id="12" Count="0" />
    </LineIds>
    <LineIds Name="FB_SlaveCtrlInstruManualTest.InstruBacklash.Get">
      <LineId Id="3" Count="3" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>